/*
 * TimeSpent Class by David Judkins
 * 
 * Used with the Remedyforce default objects: 
 * BMCServiceDesk__Incident__c (Incident)
 * BMCServiceDesk__IncidentHistory__c (Incident History)
 * 
 * When a value is entered in the default field BMCServiceDesk__timeSpent__c, the value 
 * is immediately cleared and converted into a Time Spent Incident History record.
 * 
 * PREREQUISITES: 
 * Create a custom Remedyforce Action named: 'Time Spent' 
 * Create a custom field on the Incident History object with the API Name: Time_Spent__c 
 * Create a custom setting 
 * 
 * USE:
 * 1. Create a Trigger on the BMCServiceDesk__Incident__c object
 * 2. Call TimeSpent.process() in the After Insert and After Update contexts.
 */

public class TimeSpent {
    
    //OBJECTS
    private static final String OBJECT_INCIDENT = 'BMCServiceDesk__Incident__c';
    private static final String OBJECT_INCIDENTHISTORY = 'BMCServiceDesk__IncidentHistory__c';
    
    //FIELDS
    private static final String FIELD_ID = 'Id';
    private static final String FIELD_INC_TIMESPENT = 'BMCServiceDesk__timeSpent__c';
    private static final String FIELD_IH_FKINCIDENT = 'BMCServiceDesk__FKIncident__c';
    private static final String FIELD_IH_FKACTION = 'BMCServiceDesk__FKAction__c';
    private static final String FIELD_IH_TIMESPENT = 'Time_Spent__c';
    private static final String FIELD_IH_FKSTAFF = 'BMCServiceDesk__FKUser__c';
    private static final String FIELD_INC_FKSTATUS = 'BMCServiceDesk__FKStatus__c';
       
    public static void Process(){
        
        if(TimeSpentHelper.isEnabled()){
            if(TimeSpentHelper.hasRun() != true && TimeSpentHelper.getTimeSpentActionId()!=''){ 
                TimeSpentHelper.setHasRun(); 
                
                List<sObject> incList = new List<sObject>();
                List<sObject> incHistList = new List<sObject>();
                            
                for(sObject s: Trigger.new){
                
                    if(s.get(FIELD_INC_TIMESPENT) != null){
                        TimeSpentBuilder ts = new TimeSpentBuilder(s);
                        incList.add(ts.getIncident()); 
                        incHistList.add(ts.getIncidentHistory());                                      
                    }
                }
                update incList;
                insert incHistList;            
            }
        }
        
        
    }
    
    
    
    public class TimeSpentBuilder{
        
        private sObject inc = Schema.getGlobalDescribe().get(OBJECT_INCIDENT).newSObject();
        private sObject ih = Schema.getGlobalDescribe().get(OBJECT_INCIDENTHISTORY).newSObject();
        
        public TimeSpentBuilder(sObject sObj){
            //inc
            this.inc.put( FIELD_ID,sObj.Id );
            this.inc.put( FIELD_INC_TIMESPENT,null );
            
            //ih
            this.ih.put( FIELD_IH_FKACTION, TimeSpentHelper.getTimeSpentActionId() );
            this.ih.put( FIELD_IH_FKINCIDENT, sObj.Id );
           	this.ih.put( FIELD_IH_TIMESPENT, (Decimal)sObj.get(FIELD_INC_TIMESPENT) );
            this.ih.put( FIELD_IH_FKSTAFF, UserInfo.getUserId() );
        }
        
        public sObject getIncident(){
            return inc; 
        }
        
        public sObject getIncidentHistory(){
            return ih;
        }
    }

}
